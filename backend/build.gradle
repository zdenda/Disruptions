// App Engine Backend build file

buildscript {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.google.cloud.tools:appengine-gradle-plugin:2.2.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

repositories {
    google()
    mavenCentral()
    jcenter()
}


apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'war'
apply plugin: 'com.google.cloud.tools.appengine'


dependencies {

    // define versions used for multiple dependencies
    def appEngineVersion = '1.9.79'

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':common')

    //noinspection GradleDeprecatedConfiguration providedImplementation needs Android Gradle Plugin
    providedCompile 'javax.servlet:javax.servlet-api:3.1.0'
    //providedCompile "com.google.appengine:appengine-java-sdk:$appEngineVersion"
    implementation "com.google.appengine:appengine-api-1.0-sdk:$appEngineVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    //implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.googlecode.objectify:objectify:5.1.24'
    implementation 'com.google.firebase:firebase-admin:6.12.2'
    implementation 'org.slf4j:slf4j-nop:1.7.30'

    // prevent conflict in transitive dependencies for objectify and firebase-admin
    //implementation 'io.perfmark:perfmark-api:0.20.1'

    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testImplementation 'junit:junit:4.13'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation "com.google.appengine:appengine-testing:$appEngineVersion"
    testImplementation "com.google.appengine:appengine-api-stubs:$appEngineVersion"

}

appengine {
    run {
        //jvmFlags = ['-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=5005']
        jvmFlags = ["-Ddatastore.backing_store=$rootDir/backend/local_db.bin".toString()]
    }
    deploy {
        projectId = "$googleCloudProjectId"
        version = 13
        // keep the old version running, do the switch manually in administration
        stopPreviousVersion = false
        promote = false
    }
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
