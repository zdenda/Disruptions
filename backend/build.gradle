// App Engine Backend build file

// Set the account before deploy to App Engine
// List currently available accounts:
// > gcloud auth list
// To set the active account, run:
// > gcloud config set account [ACCOUNT]

// Automatic reload of my application
// Open two terminals and run those commands
// > .\gradlew :backend:explodeWar --continuous
// > .\gradlew :backend:appengineRun

plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'com.google.cloud.tools.appengine-appenginewebxml'
}

dependencies {

    // define versions used for multiple dependencies
    def appEngineVersion = '2.0.27'

    compileOnly 'javax.servlet:javax.servlet-api:4.0.1'
    compileOnly "com.google.appengine:appengine-resources:$appEngineVersion"

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(':common')

    implementation "com.google.appengine:appengine-api-1.0-sdk:$appEngineVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'com.google.code.gson:gson:2.11.0'
    implementation 'com.googlecode.objectify:objectify:5.1.25'
    implementation 'com.google.firebase:firebase-admin:9.3.0'
    implementation 'org.slf4j:slf4j-nop:2.0.13'

    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation "com.google.appengine:appengine-testing:$appEngineVersion"
    testImplementation "com.google.appengine:appengine-api-stubs:$appEngineVersion"

}

appengine {
    run {
        //jvmFlags = ['-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=5005']
        jvmFlags = ["-Ddatastore.backing_store=$rootDir/backend/local_db.bin".toString()]
        automaticRestart = true
    }
    deploy {
        projectId = "$googleCloudProjectId"
        version = 25
        // keep the old version running, do the switch manually in administration
        stopPreviousVersion = false
        promote = false
    }
}

// JDK version which is used to build can be changed in:
// File > Settings... > Build, Execution, Deployment > Build Tools > Gradle > Gradle JDK
sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

kotlin {
    jvmToolchain(17)
}
