apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric' // Firebase Crashlytics
apply plugin: 'androidx.navigation.safeargs.kotlin'


android {
    compileSdkVersion 29
    buildToolsVersion = '29.0.2'
    defaultConfig {
        applicationId "eu.zkkn.android.disruptions"
        minSdkVersion 16
        // TODO: test on Android Q
        //noinspection OldTargetApi
        targetSdkVersion 28
        versionCode 16
        versionName "0.7.3"
        resConfigs "en", "cs"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        kapt { arguments { arg("room.schemaLocation", "$projectDir/schemas".toString()) } }
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            // SDK 21 is required for native support of Multidex
            // so this might break debug builds on older devices
            multiDexEnabled true
            aaptOptions.cruncherEnabled = false
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    // define versions used for multiple dependencies
    def roomVersion = '2.2.0'
    def lifecycleVersion = '2.2.0-beta01'
    def navVersion = navigationVersion // version of Navigation is defined in top-level build file
    def archCoreVersion = '2.1.0'

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':common')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0-beta01'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta1' // beta2 breaks layout
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'com.google.android.material:material:1.1.0-beta01'
    implementation 'com.google.firebase:firebase-core:17.2.0'
    implementation 'com.google.firebase:firebase-analytics:17.2.0'
    implementation 'com.google.firebase:firebase-messaging:20.0.0'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"
    implementation 'androidx.fragment:fragment-ktx:1.2.0-beta01'
    implementation "androidx.navigation:navigation-fragment-ktx:$navVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navVersion"
    implementation 'androidx.preference:preference-ktx:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0-beta05'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.work:work-runtime-ktx:2.2.0'
    implementation "androidx.arch.core:core-runtime:$archCoreVersion"

    debugImplementation 'com.facebook.stetho:stetho:1.5.1'

    // Android Studio reports Incorrect Scope error in unit tests with JUnit version 4.13-beta-3
    testImplementation 'junit:junit:4.12'

    androidTestImplementation "androidx.arch.core:core-testing:$archCoreVersion"
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation "androidx.room:room-testing:$roomVersion"

    kapt "androidx.room:room-compiler:$roomVersion"

}

apply plugin: 'com.google.gms.google-services'
