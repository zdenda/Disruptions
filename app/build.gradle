apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric' // Firebase Crashlytics
apply plugin: 'androidx.navigation.safeargs.kotlin'


android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "eu.zkkn.android.disruptions"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 9
        versionName "0.6.0"
        resConfigs "en", "cs"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        kapt { arguments { arg("room.schemaLocation", "$projectDir/schemas".toString()) } }
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            // SDK 21 is required for native support of Multidex
            // so this might break debug builds on older devices
            multiDexEnabled true
            aaptOptions.cruncherEnabled = false
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
}

dependencies {

    // define versions used for multiple dependencies
    def roomVersion = '2.1.0-beta01'
    def lifecycleVersion = '2.2.0-alpha01'
    def navVersion = '2.1.0-alpha03'

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':common')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha05'
    implementation 'androidx.core:core-ktx:1.2.0-alpha01'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta1'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0-beta01'
    implementation 'com.google.android.material:material:1.1.0-alpha06'
    implementation 'com.google.firebase:firebase-core:16.0.9'
    implementation 'com.google.firebase:firebase-messaging:18.0.0'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.0'
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"
    implementation 'androidx.fragment:fragment:1.1.0-alpha08'
    implementation "androidx.navigation:navigation-fragment-ktx:$navVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navVersion"
    implementation 'androidx.recyclerview:recyclerview:1.1.0-alpha05'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.work:work-runtime-ktx:2.1.0-alpha01'

    debugImplementation 'com.facebook.stetho:stetho:1.5.1'

    testImplementation 'junit:junit:4.13-beta-3'

    androidTestImplementation 'androidx.arch.core:core-testing:2.1.0-beta01'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1-beta01'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0-beta01'
    androidTestImplementation "androidx.room:room-testing:$roomVersion"

    kapt "androidx.room:room-compiler:$roomVersion"
    kapt "androidx.lifecycle:lifecycle-compiler:$lifecycleVersion"

}

apply plugin: 'com.google.gms.google-services'
