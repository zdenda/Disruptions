plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.devtools.ksp'
    id 'androidx.navigation.safeargs.kotlin'
    id 'com.google.android.gms.oss-licenses-plugin'
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics' // Firebase Crashlytics
    id 'com.google.firebase.firebase-perf' // Firebase Performance Monitoring
}

android {
    namespace 'eu.zkkn.android.disruptions'
    compileSdk 35
    buildToolsVersion = '35.0.0'
    defaultConfig {
        applicationId 'eu.zkkn.android.disruptions'
        minSdk 16
        //noinspection OldTargetApi Test on Android 14
        targetSdk 33 // Android 13
        versionCode 36
        versionName '1.1.2'
        resourceConfigurations += ['en', 'cs']
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    buildFeatures {
        viewBinding true
        buildConfig true
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            // SDK 21 is required for native support of Multidex
            // so this might break debug builds on older devices
            multiDexEnabled true
            aaptOptions.cruncherEnabled = false
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

secrets {
    propertiesFileName = "secrets.properties"
    defaultPropertiesFileName = "local.defaults.properties"
}

ksp {
    arg("room.schemaLocation", "$projectDir/schemas".toString())
}

dependencies {

    // define versions used for multiple dependencies
    def roomVersion = '2.6.1'
    def navVersion = navigationVersion // version of Navigation is defined in top-level build file
    def archCoreVersion = '2.2.0'
    def workVersion = '2.9.1'

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(':common')

    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    //noinspection GradleDependency v1.7.0 needs minSdkVersion 21
    implementation 'androidx.appcompat:appcompat:1.6.1'
    //noinspection GradleDependency v1.13.0 needs minSdkVersion 19
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    //noinspection GradleDependency v1.2.0 needs minSdkVersion 19
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    //noinspection GradleDependency v1.12.0 needs minSdkVersion 19
    implementation 'com.google.android.material:material:1.11.0'
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    //noinspection GradleDependency v2.7.0 needs minSdkVersion 19
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.6.2'
    //noinspection GradleDependency v1.7.0 needs minSdkVersion 19
    implementation 'androidx.fragment:fragment-ktx:1.6.2'
    //noinspection GradleDependency v2.8.0 needs minSdkVersion 21
    implementation "androidx.navigation:navigation-fragment-ktx:$navVersion"
    //noinspection GradleDependency v2.8.0 needs minSdkVersion 21
    implementation "androidx.navigation:navigation-ui-ktx:$navVersion"
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "androidx.work:work-runtime-ktx:$workVersion"
    implementation "androidx.work:work-gcm:$workVersion"
    implementation "androidx.arch.core:core-runtime:$archCoreVersion"
    //noinspection GradleDependency v18.0.0 needs minSdkVersion 19
    implementation 'com.google.android.gms:play-services-maps:17.0.1'
    //noinspection GradleDependency v17.1.0 needs minSdkVersion 21
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.1'

    // Import the BoM for the Firebase platform
    //noinspection GradleDependency firebase-bom v29 needs minSdkVersion 19
    implementation platform('com.google.firebase:firebase-bom:28.4.2')
    implementation 'com.google.firebase:firebase-core'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-config-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-messaging-ktx'
    implementation 'com.google.firebase:firebase-perf-ktx'


    debugImplementation 'com.facebook.stetho:stetho:1.6.0'

    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation "androidx.arch.core:core-testing:$archCoreVersion"
    //noinspection GradleDependency v1.2.0 needs minSdkVersion 19
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    //noinspection GradleDependency v3.6.0 needs minSdkVersion 19
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "androidx.room:room-testing:$roomVersion"

    ksp "androidx.room:room-compiler:$roomVersion"

}
